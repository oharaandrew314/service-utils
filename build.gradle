plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.31'
    id 'maven-publish'
    id 'jacoco'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}



dependencies {
    compileOnly platform("org.http4k:http4k-bom:4.14.1.4")
    compileOnly platform("com.amazonaws:aws-java-sdk-bom:1.11.817")
    compileOnly platform('software.amazon.awssdk:bom:2.17.85')

    compileOnly "org.jetbrains.kotlin:kotlin-stdlib"

    compileOnly group: 'org.slf4j', name: 'slf4j-api', version: '2.0.0-alpha5'
    compileOnly group: 'com.amazonaws', name: 'aws-java-sdk-dynamodb'
    compileOnly group: 'com.amazonaws', name: 'aws-java-sdk-sqs'
    compileOnly('software.amazon.awssdk:sqs')
    compileOnly "org.http4k:http4k-core"
    compileOnly "org.http4k:http4k-contract"
    compileOnly "org.http4k:http4k-format-jackson"
    compileOnly "org.http4k:http4k-format-jackson-yaml"
    compileOnly "org.http4k:http4k-format-moshi"
    compileOnly "org.http4k:http4k-format-gson"
    compileOnly 'io.split.client:java-client:4.3.0'
    compileOnly group: 'com.zaxxer', name: 'HikariCP', version: '4.0.3'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'io.kotest:kotest-runner-junit5-jvm:4.6.3'
    testImplementation group: "org.http4k", name: "http4k-testing-kotest"
    testImplementation 'com.h2database:h2:1.4.200'
    testImplementation 'com.github.oharaandrew314:mock-aws-java-sdk:1.0.0-beta.2'
}

configurations { // don't want to bundle dependencies in library, but they are needed in tests
    testImplementation.extendsFrom compileOnly
}

allprojects {
    jacoco {
        toolVersion = "0.8.7"
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.kotlin
            artifact sourcesJar
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions {
        languageVersion = "1.5"
    }
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform()
}