plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.31'
    id 'maven-publish'
    id 'jacoco'
}

ext {
    ver = [
            javalin: '3.13.0'
    ]
}

repositories {
    mavenCentral()
//    maven { url 'https://jitpack.io' }
}



dependencies {
    compileOnly platform("org.http4k:http4k-bom:4.14.1.4")
    compileOnly platform("com.amazonaws:aws-java-sdk-bom:1.11.817")

    compileOnly "org.jetbrains.kotlin:kotlin-stdlib"

    compileOnly group: 'io.javalin', name: 'javalin', version: ver.javalin
    compileOnly group: 'io.javalin', name: 'javalin-openapi', version: ver.javalin
    compileOnly group: 'org.slf4j', name: 'slf4j-api', version: '2.0.0-alpha5'
    compileOnly group: 'com.amazonaws', name: 'aws-java-sdk-dynamodb'
    compileOnly "org.http4k:http4k-contract"
    compileOnly "org.http4k:http4k-format-jackson"
    compileOnly "org.http4k:http4k-format-jackson-yaml"
    compileOnly "org.http4k:http4k-format-moshi"
    compileOnly "org.http4k:http4k-format-gson"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'io.kotest:kotest-runner-junit5-jvm:4.6.3'
//    testImplementation 'com.github.javalin:javalin-test:0.4.0'
    testImplementation group: "org.http4k", name: "http4k-testing-kotest"
    testImplementation 'com.h2database:h2:1.4.197'
}

configurations { // don't want to bundle dependencies in library, but they are needed in tests
    testImplementation.extendsFrom compileOnly
}

jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.kotlin
            artifact sourcesJar
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
    kotlinOptions {
        languageVersion = "1.5"
    }
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

test {
    useJUnitPlatform()
}